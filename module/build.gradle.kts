import android.databinding.tool.ext.capitalizeUS
import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens
import java.security.MessageDigest

plugins {
    alias(libs.plugins.agp.app)
    alias(libs.plugins.lsplugin.cmaker)
}

val moduleId: String by rootProject.extra
val moduleName: String by rootProject.extra
val verCode: Int by rootProject.extra
val verName: String by rootProject.extra
val commitHash: String by rootProject.extra
val abiList: List<String> by rootProject.extra
val androidMinSdkVersion: Int by rootProject.extra
val author: String by rootProject.extra
val description: String by rootProject.extra
val moduleDescription = description

android {
    defaultConfig {
        ndk {
            abiFilters.addAll(abiList)
        }
    }

    buildFeatures {
        prefab = true
    }

    externalNativeBuild {
        cmake {
            version = "3.28.0+"
            path("src/main/cpp/CMakeLists.txt")
        }
    }
}

cmaker {
    default {
        arguments += arrayOf(
            "-Wno-dev",
            "-DANDROID_STL=none",
            "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON",
            "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=ON",
            "-DMODULE_NAME=$moduleId",
            "-DCMAKE_CXX_STANDARD=23",
            "-DCMAKE_C_STANDARD=23",
            "-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON",
            "-DCMAKE_VISIBILITY_INLINES_HIDDEN=ON",
            "-DCMAKE_CXX_VISIBILITY_PRESET=hidden",
            "-DCMAKE_C_VISIBILITY_PRESET=hidden",
            )
        abiFilters(*abiList.toTypedArray())
    }
}

dependencies {
    implementation(libs.cxx)
}

evaluationDependsOn(":service")

// Rust Build Integration

// Ensure cargo-ndk is installed
tasks.register<Exec>("installCargoNdk") {
    group = "rust"
    description = "Installs cargo-ndk if not present"
    commandLine("bash", "-c", "cargo ndk --version >/dev/null 2>&1 || cargo install cargo-ndk")
}

// Ensure Rust Android targets are installed
tasks.register<Exec>("installRustTargets") {
    group = "rust"
    description = "Installs Android Rust targets via rustup"
    // We run rustup target add for all required targets.
    // Ideally we should check if they are installed, but `rustup target add` is idempotent and fast if already installed.
    commandLine(
        "rustup", "target", "add",
        "aarch64-linux-android",
        "armv7-linux-androideabi",
        "x86_64-linux-android",
        "i686-linux-android"
    )
    // Ensure cargo-ndk is installed first (though not strictly dependent, good for ordering)
    dependsOn("installCargoNdk")
}

tasks.register<Exec>("cargoBuild") {
    group = "rust"
    description = "Builds the Rust static library for all Android targets using cargo-ndk"
    workingDir = file("../rust/cbor-cose")

    dependsOn("installRustTargets")

    // Using cargo-ndk to build for all supported ABIs.
    commandLine(
        "cargo", "ndk",
        "-t", "arm64-v8a",
        "-t", "armeabi-v7a",
        "-t", "x86_64",
        "-t", "x86",
        "build", "--release"
    )

    doLast {
        // Manually copy static libraries to where CMake expects them
        copy {
            from("../rust/cbor-cose/target/aarch64-linux-android/release/libcleverestricky_cbor_cose.a")
            into("src/main/cpp/external/rust_libs/arm64-v8a")
        }
        copy {
            from("../rust/cbor-cose/target/armv7-linux-androideabi/release/libcleverestricky_cbor_cose.a")
            into("src/main/cpp/external/rust_libs/armeabi-v7a")
        }
        copy {
            from("../rust/cbor-cose/target/x86_64-linux-android/release/libcleverestricky_cbor_cose.a")
            into("src/main/cpp/external/rust_libs/x86_64")
        }
        copy {
            from("../rust/cbor-cose/target/i686-linux-android/release/libcleverestricky_cbor_cose.a")
            into("src/main/cpp/external/rust_libs/x86")
        }
    }
}

// Hook into preBuild to ensure Rust libs are ready before CMake runs
tasks.named("preBuild") {
    dependsOn("cargoBuild")
}

afterEvaluate {
    android.buildTypes.forEach { buildType ->
        val variantLowered = buildType.name.lowercase()
        val variantCapped = buildType.name.capitalizeUS()
        val buildTypeCapped = buildType.name.replaceFirstChar { it.uppercase() }
        val buildTypeLowered = buildType.name.lowercase()
        val supportedAbis = abiList.map {
            when (it) {
                "arm64-v8a" -> "arm64"
                "armeabi-v7a" -> "arm"
                "x86" -> "x86"
                "x86_64" -> "x64"
                else -> error("unsupported abi $it")
            }
        }.joinToString(" ")

        val moduleDir = layout.buildDirectory.file("outputs/module/$variantLowered")
        val zipFileName =
            "$moduleName-$verName-$verCode-$commitHash-$buildTypeLowered.zip".replace(' ', '-')

        val prepareModuleFilesTask = tasks.register<Sync>("prepareModuleFiles$variantCapped") {
            group = "module"
            dependsOn(
                "assemble$variantCapped",
                ":service:package$buildTypeCapped"
            )
            into(moduleDir)
            from(rootProject.layout.projectDirectory.file("README.md"))
            from(layout.projectDirectory.file("template")) {
                exclude("module.prop", "customize.sh", "post-fs-data.sh", "service.sh", "daemon", "provision_attestation.sh")
                filter<FixCrLfFilter>("eol" to FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from(layout.projectDirectory.file("template")) {
                include("module.prop")
                expand(
                    "moduleId" to moduleId,
                    "moduleName" to moduleName,
                    "versionName" to "$verName ($verCode-$commitHash-$variantLowered)",
                    "versionCode" to verCode,
                    "author" to author,
                    "description" to moduleDescription,
                )
            }
            from(layout.projectDirectory.file("template")) {
                include("customize.sh", "post-fs-data.sh", "service.sh", "daemon", "provision_attestation.sh")
                val tokens = mapOf(
                    "DEBUG" to if (buildTypeLowered == "debug") "true" else "false",
                    "SONAME" to moduleId,
                    "SUPPORTED_ABIS" to supportedAbis,
                    "MIN_SDK" to androidMinSdkVersion.toString()
                )
                filter<ReplaceTokens>("tokens" to tokens)
                filter<FixCrLfFilter>("eol" to FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from(project(":service").tasks.getByName("package$buildTypeCapped").outputs) {
                include("*.apk")
                rename(".*\\.apk", "service.apk")
            }
            from(layout.buildDirectory.file("intermediates/stripped_native_libs/$variantLowered/strip${variantCapped}DebugSymbols/out/lib")) {
                exclude("**/libbinder.so", "**/libutils.so")
                into("lib")
            }

            from(layout.buildDirectory.dir("intermediates/cxx")) {
                include("**/inject")
                eachFile {
                    val segments = relativePath.segments
                    // For release builds, we might have RelWithDebInfo, Release, or MinSizeRel directories
                    // For debug builds, we expect Debug directory
                    if (buildTypeLowered == "release" && segments.contains("Debug")) {
                        exclude()
                        return@eachFile
                    }
                    if (buildTypeLowered == "debug" && !segments.contains("Debug")) {
                        exclude()
                        return@eachFile
                    }

                    val abi = segments.find { it in abiList }
                    if (abi != null) {
                        relativePath = RelativePath(true, "lib", abi, "inject")
                    }
                }
                includeEmptyDirs = false
            }

            doLast {
                val apk = file("${moduleDir.get().asFile}/service.apk")
                if (!apk.exists() || apk.length() == 0L) {
                    throw GradleException("service.apk is missing or empty!")
                }

                abiList.forEach { abi ->
                    val injectPath = file("${moduleDir.get().asFile}/lib/$abi/inject")
                    if (!injectPath.exists()) {
                        throw GradleException("inject binary for $abi is missing at $injectPath")
                    }
                }

                fileTree(moduleDir).visit {
                    if (isDirectory) return@visit
                    val md = MessageDigest.getInstance("SHA-256")
                    file.forEachBlock(4096) { bytes, size ->
                        md.update(bytes, 0, size)
                    }
                    file(file.path + ".sha256").writeText(
                        org.apache.commons.codec.binary.Hex.encodeHexString(
                            md.digest()
                        )
                    )
                }
            }
        }

        val zipTask = tasks.register<Zip>("zip$variantCapped") {
            group = "module"
            dependsOn(prepareModuleFilesTask)
            archiveFileName.set(zipFileName)
            destinationDirectory.set(layout.projectDirectory.file("release").asFile)
            from(moduleDir)
        }

        val pushTask = tasks.register<Exec>("push$variantCapped") {
            group = "module"
            dependsOn(zipTask)
            doFirst {
                commandLine("adb", "push", zipTask.get().outputs.files.singleFile.path, "/data/local/tmp")
            }
        }

        val installKsuTask = tasks.register<Exec>("installKsu$variantCapped") {
            group = "module"
            dependsOn(pushTask)
            commandLine(
                "adb", "shell", "su", "-c",
                "/data/adb/ksud module install /data/local/tmp/$zipFileName"
            )
        }

        val installMagiskTask = tasks.register<Exec>("installMagisk$variantCapped") {
            group = "module"
            dependsOn(pushTask)
            commandLine(
                "adb",
                "shell",
                "su",
                "-M",
                "-c",
                "magisk --install-module /data/local/tmp/$zipFileName"
            )
        }

        tasks.register<Exec>("installKsuAndReboot$variantCapped") {
            group = "module"
            dependsOn(installKsuTask)
            commandLine("adb", "reboot")
        }

        tasks.register<Exec>("installMagiskAndReboot$variantCapped") {
            group = "module"
            dependsOn(installMagiskTask)
            commandLine("adb", "reboot")
        }
    }
}
